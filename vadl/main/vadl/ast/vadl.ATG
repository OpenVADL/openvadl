$package=vadl.ast
import java.util.ArrayList;
import java.util.HashMap;
import java.util.List;
import javax.annotation.Nullable;

import vadl.ast.Ast;
import vadl.ast.Expr;
import vadl.ast.Definition;
import static vadl.ast.CounterDefinition.CounterKind.GROUP;
import static vadl.ast.CounterDefinition.CounterKind.PROGRAM;
import vadl.utils.SourceLocation;

import vadl.types.BitsType;
import vadl.types.BoolType;
import vadl.types.Type;
import vadl.types.SIntType;
import vadl.types.UIntType;


COMPILER vadl
    boolean insideMacro = false;
    Ast ast = new Ast();
    SymbolTable currentSymbolTable = new SymbolTable();
    Ungrouper ungrouper = new Ungrouper();

    /**
    * Converts a coco token to a vadl location.
    */
    SourceLocation locationFromToken(Token token) {
        return new SourceLocation(
            SourceLocation.INVALID_SOURCE_LOCATION.uri(),
            new SourceLocation.Position(token.line, token.col),
            new SourceLocation.Position(token.line, token.col + t.val.length()));
    }

    /**
    *   Reorders wrongly parsed binary expressions
    */
    Expr reorderBinary(Expr expr) {
        // Only if not inside model parsing.
        // Cause there we don't know yet what the order is.
        if (insideMacro || !(expr instanceof BinaryExpr)) {
            return expr;
        }

        // FIXME: This doesn't completely work for model instantiation
        return BinaryExpr.reorder((BinaryExpr) expr);
    }

    /**
    * Removes all group expressions.
    */
    Expr ungroup(Expr expr) {
        //  Only if not inside model parsing.
        if (insideMacro) {
          return expr;
        }

        return ungrouper.ungroup(expr);
    }

    /**
    * Expands a macro.
    */
    @Nullable Node expandMacro(Identifier identifier, List<Node> args, SyntaxType requiredReturnType) {
        var unexpanded = new PlaceHolderExpr(identifier, args, identifier.location());
        if (insideMacro) {
            return unexpanded;
        }

        var macro = currentSymbolTable.getMacro(identifier.name);
        if (macro == null) {
           errors.SemErr(t.line, t.col, "No macro named `%s` exists.".formatted(identifier.name));
           return unexpanded;
        }

        // The macro itself was invalid but an error for it was already issued so we silently abort the expansion here.
        if (macro.returnType() == CoreType.Invalid()) {
           return unexpanded;
        }

        boolean hasError = false;

        // Verify the arguments
        if (macro.params().size() != args.size()) {
           errors.SemErr(t.line, t.col, "The macro `%s` expects %d arguments but %d were provided.".formatted(identifier.name, macro.params().size(), args.size()));
           hasError = true;
        }

        var argMap = new HashMap<String, Node>();
        if (!hasError)
        {
            for (int i = 0; i < args.size(); i ++) {
                var arg = args.get(i);
                var param = macro.params().get(i);
                var argType = arg.syntaxType();

                if (!argType.isSubTypeOf(param.type())) {
                    errors.SemErr(t.line, t.col, "The macro's `%s` parameter `%s` expects a `%s` but the argument provided is of type `%s`.".formatted(identifier.name, param.name().name, param.type(), argType));
                    hasError = true;
                }
                argMap.put(param.name().name, arg);
            }
        }

        // Verify the return type
        if (!macro.returnType().isSubTypeOf(requiredReturnType)) {
           errors.SemErr(t.line, t.col, "The macro `%s` returns `%s` but here a macro returning `%s` is expected.".formatted(identifier.name, macro.returnType(), requiredReturnType));
           hasError = true;
        }

        if (hasError) {
           return unexpanded;
        }

         // FIXME: There should be a real instantziator here
         var body = macro.body();
         if (body instanceof Expr) {
            var expander = new MacroExpander();
            body = expander.expandExpr((Expr)body, argMap);
            body = new GroupExpr((Expr) body);
         } else {
            throw new RuntimeException("Expanding non expressions are not yet implemented");
         }
         return body;
    }


    /**
    * A simple lookahead helper that returns true if the token at the nth position matches the provided string.
    *
    * @param lookAheadPos the position to lookahead 0 is the next token.
    * @param value to match
    */
    boolean matchN(int lookAheadPos, String value) {
        for(int i = 0; i <= lookAheadPos -2; i++) {
            var t = scanner.Peek();
            System.out.println("token %s".formatted(t.val));
        }
        return scanner.Peek().val.equals(value);
    }

    void pushScope() {
        currentSymbolTable = currentSymbolTable.createChild();
    }

    void popScope() {
        currentSymbolTable = currentSymbolTable.parent;
    }

    SourceLocation loc() {
        return locationFromToken(t);
    }

CHARACTERS
    digit = '0' .. '9'.
    binDigit = '0' .. '1'.
    hexDigit = '0' .. '9' + 'A' .. 'F' + 'a' .. 'f'.
    letter = 'a' .. 'z' + 'A' .. 'Z'.

TOKENS
    number = digit {digit | "'"}
           | "0x" hexDigit {hexDigit | "'"}
           | "0b" binDigit {binDigit | "'"}
           .
    identifierToken = letter {letter | digit | '_'}.
    file = "file".

COMMENTS FROM "//" TO '\n'
COMMENTS FROM "/*" TO "*/" NESTED

IGNORE '\t' + '\r' + '\n'


PRODUCTIONS
    vadl =
    {
        topLevelDefinitions<out Definition d>   (. ast.definitions.add(d); .)
        | macroDef
    }.

    // -- DEFINITIONS --------------------------------------------------------------------------------------------------
    topLevelDefinitions<out Definition definition>                      (. definition = null; .)
    = instructionsetDefinition<out InstructionSetDefinition isaDef>        (. definition  = isaDef; .)
    | commonDefinitionSection<out Definition def>                       (. definition  = def; .)
    .

    commonDefinitionSection<out Definition definition>              (. definition = null; .)
    = constantDefinition<out Definition def>                        (. definition = def; def.accept(currentSymbolTable); .)
    | formatDefinition<out Definition def>                          (. definition = def; def.accept(currentSymbolTable); .)
    .

    instructionsetDefinition<out InstructionSetDefinition isaDef>   (. var children = new ArrayList<Definition>(); pushScope(); .)
    = "instruction"                                                 (. var startLocation = loc(); .)
       "set" "architecture" identifier<out Identifier identifier>
       "=" "{"
      {
        isaDefinition<out Definition def>                           (. children.add(def); def.accept(currentSymbolTable); .)
      }
      "}"                                                           (. isaDef = new InstructionSetDefinition(identifier, children, startLocation.join(loc()));
                                                                       popScope();
                                                                       isaDef.accept(currentSymbolTable);
                                                                     .)


      .

    isaDefinition<out Definition def>                       (. def = null; .)
    = constantDefinition<out Definition d>                  (. def = d; .)
    | formatDefinition<out Definition d>                    (. def = d; .)
    | counterDefinition<out Definition d>                   (. def = d; .)
    | instructionDefinition<out Definition d>               (. def = d; .)
    | memoryDefinition<out Definition d>                    (. def = d; .)
    | IF (scanner.Peek().kind == _file) registerFileDefinition<out Definition d>   (. def = d; .)
    | registerDefinition<out Definition d>   (. def = d; .)
    .

    constantDefinition<out ConstantDefinition def>      (. TypeLiteral type = null; .)
    = "constant"                                        (. var startLocation = loc(); .)
        identifier<out Identifier id>
        [
            ':' typeLiteral<out TypeLiteral t1>         (. type = t1; .)
        ]
        '=' expression<out Expr expr>                   (. def = new ConstantDefinition(id, type, expr, startLocation.join(loc())); .)
    .

    formatDefinition<out FormatDefinition def>                      (. List<FormatDefinition.FormatField> fields = new ArrayList<>();.)
    = "format"                                                      (. var startLoc = loc(); .)
    identifier<out Identifier id>
    ":" typeLiteral<out TypeLiteral type>
    ["=" "{"
        formatField<out FormatDefinition.FormatField f1>            (. fields.add(f1); .)
        {
        "," formatField<out FormatDefinition.FormatField f2>        (. fields.add(f2); .)
        }
    "}"]                                                            (. def = new FormatDefinition(id, type, fields, startLoc.join(loc())); .)
    .

    // FIXME: more versions are missing here
    formatField<out FormatDefinition.FormatField field>
    = identifier<out Identifier id>  "["            (. List<RangeExpr> ranges = new ArrayList<>();.)
        rangeExpression<out RangeExpr e1>           (. ranges.add(e1); .)
        {
            "," rangeExpression<out RangeExpr e2>   (. ranges.add(e2); .)
        }
    "]"                                             (. field = new FormatDefinition.FormatField(id, ranges); .)
    .

    counterDefinition<out CounterDefinition def>    (. CounterDefinition.CounterKind kind = null; .)
    = ("program"                                    (. kind = PROGRAM; .)
    |  "group"                                      (. kind = GROUP; .)
    )                                               (. var startLocation = loc(); .)
    "counter"
        identifier<out Identifier id>
        ":" typeLiteral<out TypeLiteral t1>         (. def = new CounterDefinition(kind, id, t1, startLocation.join(loc())); .)
    .

    instructionDefinition<out InstructionDefinition def> (. var startLocation = loc(); .)
    = "instruction" identifier<out Identifier id>
      ":" identifier<out Identifier formatIdentifier>    (. pushScope(); currentSymbolTable.loadFormat(formatIdentifier); .)
      "=" blockStatement<out BlockStatement behavior>    (. def = new InstructionDefinition(id, formatIdentifier, behavior, loc());
                                                            popScope(); .)
    .

    memoryDefinition<out MemoryDefinition def>
    = "memory"                                  (. var startLocation = loc(); .)
        identifier<out Identifier id>
        ":" typeLiteral<out TypeLiteral t1>
        "->" typeLiteral<out TypeLiteral t2>    (. def = new MemoryDefinition(id, t1, t2, startLocation.join(t2.location())); .)
    .

    registerDefinition<out RegisterDefinition def>
        = "register"                                        (. var startLocation = loc(); .)
          identifier<out Identifier id>
          ":" typeLiteral<out TypeLiteral t1>               (. def = new RegisterDefinition(id, t1, startLocation.join(t1.location())); .)
    .

    registerFileDefinition<out Definition def>
        = "register"                                        (. var startLocation = loc(); .)
              "file"
              identifier<out Identifier id>
              ":" typeLiteral<out TypeLiteral t1>
              "->" typeLiteral<out TypeLiteral t2>          (. def = new RegisterFileDefinition(id, t1, t2, startLocation.join(t2.location())); .)
    .


    // -- STATEMENTS ---------------------------------------------------------------------------------------------------

    statement<out Statement statement>         (. statement = null; .)
    = letStatement<out Statement letStatement> (. statement = letStatement; .)
    | ifStatement<out Statement ifStatement>   (. statement = ifStatement; .)
    | assignment<out Statement assignment>     (. statement = assignment; .)
    | blockStatement<out Statement block>      (. statement = block; .)
    .

    blockStatement<out BlockStatement block>   (. block = new BlockStatement(); pushScope(); .)
    = "{" {
        statement<out Statement statement>     (. block.add(statement); .)
     } "}"                                     (. popScope(); .)
    .

    assignment<out Statement statement>
    = identifier<out Identifier id> ":=" expression<out Expr expr> (. statement = new AssignmentStatement(id, expr); .)
    .

    letStatement<out Statement letStatement>
    = "let" identifier<out Identifier id>   (. pushScope(); currentSymbolTable.defineConstant(id.name, loc()); .)
      "=" expression<out Expr valueExpr>
      "in" statement<out Statement body>    (. letStatement = new LetStatement(id, valueExpr, body); popScope(); .)
    .

    ifStatement<out Statement ifStatement>
    = "if" expression<out Expr condition>
      "then" statement<out Statement thenStmt>     (. ifStatement = new IfStatement(condition, thenStmt, null);     .)
      [ "else" statement<out Statement elseStmt>   (. ifStatement = new IfStatement(condition, thenStmt, elseStmt); .) ]
    .

    // -- EXPRESSIONS --------------------------------------------------------------------------------------------------

    expression<out Expr expr>               (. expr = null; .)
    = binary<out Expr expr1>                (. expr = ungroup(expr1); .)
    | unary<out Expr expr1>                 (. expr = ungroup(expr1); .)
    .

    binary<out Expr expr>
    =  term<out Expr expr1>                              (. expr = expr1; .)
        {
            binaryOperator <out Operator op>
            term<out Expr expr2>                         (. expr = new BinaryExpr(expr, op, expr2); .)
        }                                                   (. expr = reorderBinary(expr); .)
    .

    unary<out Expr expr>
    = unaryOperator<out UnaryOperator op> term<out Expr operand> (. expr = new UnaryExpr(op, operand); .)
    .

    unaryOperator<out UnaryOperator op> (. op = null; .)
    = "-" (. op = UnaryOperator.NEGATIVE; .)
    | "!" (. op = UnaryOperator.LOG_NOT; .)
    | "~" (. op = UnaryOperator.COMPLEMENT; .)
    .

    binaryOperator <out Operator op>    (.op = null;.)
    = "||"  (. op = Operator.LogicalOr(); .)
    | "&&"  (. op = Operator.LogicalAnd(); .)
    | "|"   (. op = Operator.Or(); .)
    | "^"   (. op = Operator.Xor(); .)
    | "&"   (. op = Operator.And(); .)
    | "="   (. op = Operator.Equal(); .)
    | "!="  (. op = Operator.NotEqual(); .)
    | ">="  (. op = Operator.GreaterEqual(); .)
    | ">"   (. op = Operator.Greater(); .)
    | "<="  (. op = Operator.LessEqual(); .)
    | "<"   (. op = Operator.Less(); .)
    | "<>>" (. op = Operator.RotateRight(); .)
    | "<<>" (. op = Operator.RotateLeft(); .)
    | ">>"  (. op = Operator.ShiftRight(); .)
    | "<<"  (. op = Operator.ShiftLeft(); .)
    | "+"   (. op = Operator.Add(); .)
    | "-"   (. op = Operator.Subtract(); .)
    | "*"   (. op = Operator.Multiply(); .)
    | "/"   (. op = Operator.Divide(); .)
    | "%"   (. op = Operator.Modulo(); .)
    .

    term<out Expr expr>                             (. expr = null; .)
    = number                                        (. expr = new VerbatimLiteral(t.val, loc()); .)
    | identifier<out Identifier id>                 (. expr = new Variable(id); currentSymbolTable.requireValue(id.name, loc()); .)
    | '(' expression<out Expr expr1> ')'            (. expr = new GroupExpr(expr1); .)
    | macroInstanceOrPlaceholder<out Node node1, CoreType.Ex()>  (. expr = (Expr)node1; .)
    .

    rangeExpression<out RangeExpr expr>
    = expression<out Expr e1> ".." expression<out Expr e2> (. expr = new RangeExpr(e1, e2);.)
    .

    // -- PRIMETIVES ---------------------------------------------------------------------------------------------------

    typeLiteral<out TypeLiteral type>       (. type = null; Expr sizeExpr = null; .)
    =
    "Bool"                                  (. var loc = loc(); type = new TypeLiteral(new Identifier(t.val, loc), null, loc); .)
    |
     (
        identifier<out Identifier id>
        [ "<"
            // FIXME: There is a LL(1) conflict if switching ot expression
            term<out Expr expr>             (. sizeExpr = expr; .)
        ">" ]                               (. type = new TypeLiteral(id, sizeExpr, id.location().join(loc())) ;.)
    )
    .

    identifier<out Identifier identifier> = identifierToken (. identifier = new Identifier(t.val, loc()); .)
    .

    // -- MACROS -------------------------------------------------------------------------------------------------------
    macroInstanceOrPlaceholder<out Node body, SyntaxType requiredType>      (. body = null; .)
    = IF (!insideMacro) macroInstance<out Node b, requiredType>             (. body = b; .)
    | placeHolder<out Node b, requiredType>                                 (. body = b;.)
    .

    placeHolder<out Node body, SyntaxType requiredType>
    = "$"                                               (. var startloc = loc(); var args = new ArrayList<Node>(); .)
    identifier<out Identifier id>
    [
        "("
            [
                expression<out Expr arg1>               (. args.add(arg1); .)
                {
                    ";" expression<out Expr arg2>       (.args.add(arg2); .)
                }
            ]
        ")"
    ]                                                   (. body = new PlaceHolderExpr(id, args, startloc.join(loc())); .)
    .


    macroInstance<out Node body, SyntaxType requiredType>  = "$" identifier<out Identifier id>
    "("                                             (. var args = new ArrayList<Node>(); .)
        [
            expression<out Expr arg1>               (. args.add(arg1); .)
            {
                ";" expression<out Expr arg2>       (.args.add(arg2); .)
            }
        ]
    ")"         (. body = expandMacro(id, args, requiredType); .)
    .

    macroDef
    = "model"                           (. var startLoc = loc(); .)
    identifier<out Identifier id>
    "("                                 (. var params = new ArrayList<MacroParam>(); .)
        [
            identifier<out Identifier paramId1> ":" syntaxType<out SyntaxType synType1> (. params.add(new MacroParam(paramId1, synType1)); .)
            {
                ","
                identifier<out Identifier paramId2> ":" syntaxType<out SyntaxType synType2> (. params.add(new MacroParam(paramId2, synType2)); .)
            }
        ]
     ")"
    ":" syntaxType<out SyntaxType returnType> "="
    "{"                                 (. Node body = null; insideMacro = true; .)
        expression<out Expr bodyEx>     (. body = bodyEx; .)
    "}"                                 (.
                                            insideMacro = false;
                                            var bodyType = body.syntaxType();
                                            if (!bodyType.isSubTypeOf(returnType)) {
                                                errors.SemErr(t.line, t.col, "The macro signature promises to return `%s` but it's body is of type `%s`.".formatted(returnType, bodyType));
                                                currentSymbolTable.addMacro(new Macro(id, params, body, CoreType.Invalid()), startLoc.join(loc()));
                                            } else {
                                                currentSymbolTable.addMacro(new Macro(id, params, body, returnType), startLoc.join(loc()));
                                            }
                                         .)
    .

    syntaxType<out SyntaxType typ> = syntaxCoreType<out CoreType coreType> (. typ = coreType; .)
    .

    syntaxCoreType<out CoreType typ>  (. typ = null; .)
    = "Stats"       (. typ = CoreType.Stats(); .)
    | "Stat"        (. typ = CoreType.Stat(); .)
    | "Encs"        (. typ = CoreType.Encs(); .)
    | "IsaDefs"     (. typ = CoreType.IsaDefs(); .)
    | "Ex"          (. typ = CoreType.Ex(); .)
    | "Lit"         (. typ = CoreType.Lit(); .)
    | "Str"         (. typ = CoreType.Str(); .)
    | "Val"         (. typ = CoreType.Val(); .)
    | "Bool"        (. typ = CoreType.Bool(); .)
    | "Int"         (. typ = CoreType.Int(); .)
    | "Bin"         (. typ = CoreType.Bin(); .)
    | "CallEx"      (. typ = CoreType.CallEx(); .)
    | "SymEx"       (. typ = CoreType.SymEx(); .)
    | "Id"          (. typ = CoreType.Id(); .)
    | "BinOp"       (. typ = CoreType.BinOp(); .)
    | "UnOp"        (. typ = CoreType.UnOp(); .)
    .



END vadl.
